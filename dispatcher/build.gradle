apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Manifest version information
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

ext {
    bintrayRepo = 'maven'
    bintrayName = 'dispatcher'

    publishedGroupId = 'com.robotpajamas.dispatcher'
    libraryName = 'Dispatcher'
    artifact = 'dispatcher'

    libraryDescription = 'Dispatching things in a Dispatchable way'

    siteUrl = 'https://github.com/RobotPajamas/Dispatcher'
    gitUrl = 'https://github.com/RobotPajamas/Dispatcher.git'

    libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}"

    developerId = 'sureshjoshi'
    developerName = 'Suresh Joshi'
    developerEmail = 'suresh@robotpajamas.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.10.0'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation "org.robolectric:robolectric:3.8"
}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/sureshjoshi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/sureshjoshi/JCenter/master/bintrayv1.gradle'
}

tasks.withType(Javadoc) {
    enabled = false
}
